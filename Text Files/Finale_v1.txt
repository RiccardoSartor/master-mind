5 rem **** variabili e altro *****
10 min = 1
11 max = 6
12 t = 9
13 l = 4

50 poke 53281, 11 : rem sfondo
51 poke 53280, 12 : rem bordi esterni schermo

100 rem ***** inizio *****
105 ? chr$(147)
110 for i=1 to 9 : ? : next i
120 poke 646, 13
125 ?tab(13);"**************"
130 ?tab(13);"*            *"
135 ?tab(13);"* ";
136 poke 646, 3
137 ?"mastermind ";
138 poke 646, 13
139 ?"*"
140 ?tab(13);"*            *"
145 ?tab(13);"**************"
146 poke 646, 14
150 for i=1 to 6 : ? : next i
155 poke 646, 14
160 ?tab(4); "premere un tasto per continuare"
170 get a$ : if a$="" then 170

200 rem ***** menu *****
215 ?chr$(147)
220 poke 646, 13
225 ?tab(11);"******************"
230 ?tab(11);"* ";
235 poke 646, 3
240 ?"menu' di gioco ";
245 poke 646, 13
250 ?"*"
255 ?tab(11);"******************"
260 poke 646, 14
265 ?:?" premere:"
270 ?:?" 1-impostazioni di gioco"
272 ?:?" 2-regole di gioco"
274 ?:?" 3-gioca"
276 ?:?" 4-esci dal gioco"
278 get a$ : if a$="" then 278
280 if a$="1" then 300:rem goto impostazioni
285 if a$="2" then 600:rem goto regole
290 if a$="3" then 900:rem goto gioco
295 if a$="4" then 10000:rem esci dal gioco

300 rem ***** impostazioni *****
305 ?chr$(147)
310 poke 646, 13
315 ?tab(12);"****************"
320 ?tab(12);"* ";:poke 646, 3:?"impostazioni";:poke 646, 13:?" *"
325 ?tab(12);"****************"
330 poke 646, 14
340 ?:?" premere:"
345 ?:?" 1-cambia il num minimo (ora:";min;")"
346 ?:?" 2-cambia il num massimo (ora:";max;")"
347 ?:?" 3-cambia il num di tentativi (ora:";t;")"
348 ?:?" 4-torna al menu' principale"
360 get a$ : if a$="" then 360
370 if a$="1" then 400:rem modifica min
371 if a$="2" then 450:rem modifica max
372 if a$="3" then 500:rem modifica tentativi
373 if a$="4" then 200:rem torna al menu

400 rem ***** modifica min *****
401 ?:?" ***** modifica minimo *****"
402 ?" valore default: 1"
405 ?" il valore deve essere tra 1 e";max-1
410 input" inserisci il numero";b
415 if (b<1 or b>(max-1)) then 405 : rem non corretto
420 min = b : rem else cambia
425 goto 300

450 rem ***** modifica max *****
451 ?:?" ***** modifica massimo *****"
452 ?" valore default: 6"
455 ?" il valore deve essere tra";min+1;"e 9"
460 input" inserisci il numero";b
465 if (b<min+1 or b>9) then 455 : rem non corretto
470 max = b : rem else cambia
475 goto 300

500 rem ***** modifica tentativi ******
501 ?:?" ***** modifica tentativi *****"
502 ?" valore default: 9"
505 ?" il valore deve essere tra 1 e 9"
510 input" inserisci il numero";b
515 if (b<1 or b>9) then 505 : rem non corretto
520 t = b : rem else cambia
525 goto 300

600 rem ***** regole *****
615 ?chr$(147)
620 poke 646, 13
625 ?tab(10);"********************"
630 ?tab(10);"*";
635 poke 646, 3
640 ?" istruzioni gioco";
645 poke 646, 13
650 ?" *"
655 ?tab(10);"********************"
660 poke 646, 14

665 ?:?;" ************* obiettivo: *************"
670 poke 646, 0: ?" Q";
675 poke 646, 14
680 ?"indovinare il codice segreto"

685 ?:?" ********** cose da sapere: ***********"
690 poke 646, 1: ?" Q";
695 poke 646, 14
700 ?"hai";t;"tentativi"
705 ?:poke 646, 0: ?" Q";
710 poke 646, 14
715 ?"un pallino nero corrisponde ad una"
720 ?"  cifra inserita nella posizione giusta"
725 ?:poke 646, 1: ?" Q";
730 poke 646, 14
735 ?"un pallino bianco corrisponde ad una"
740 ?"  cifra giusta inserita nella posizione"
745 ?"  sbagliata"
750 ?:poke 646, 0: ?" Q";
755 poke 646, 14
760 ?"nessun pallino corrisponde ad una"
765 ?"  cifra sbagliata"
770 ?:?:?tab(2); "premere un tasto per tornare al menu"
775 get a$ : if a$="" then 775
780 goto 200

900 rem ***** gioco *****
910 rem ***** generazione codice casuale *****
920 x = rnd(-ti) : rem inizializzazione random
930 for i=1 to l
940 c(i)=int(rnd(1)*max)+min
950 nc = nc*10+c(i) : rem Creazione del numero generato per facilitare la stampa
960 next i
970 rem ?"numero casuale: "; nc : rem stampa codice generato

1000 rem ***** preparazione bordi *****
1010 b1$=chr$(155)+"UCCC"+chr$(178)+"CCCCCC"+chr$(178)+"CCCCCCI"
1020 b2$=chr$(155)+chr$(171)+"CCC"+chr$(123)
1025 b2$=b2$+"CCCCCC"+chr$(123)+"CCCCCC"+chr$(179)
1030 b3$=chr$(155)+"JCCC"+chr$(177)+"CCCCCC"+chr$(177)+"CCCCCCK"
1040 r1$=chr$(155)+"B "+chr$(154)+"t "+chr$(155)+"B "+chr$(154)
1045 r1$=r1$+"cod: "+chr$(155)+"B "+chr$(154)+"ris: "+chr$(155)+"B"
1060 s0$=chr$(155)+"B   B      B      B"

1100 rem ***** stampa tabella *****
1105 ?chr$(147)
1110 ?tab(20);b1$
1120 ?tab(20);s0$
1130 ?tab(20);r1$
1140 ?tab(20);s0$
1150 for i=1 to t
1160 ?tab(20);b2$
1170 ?tab(20);s0$
1180 next i
1190 ?tab(20);b3$

1200 rem ***** inizio input *****
1205 poke 646, 14

1207 rem spostare all'inizio
1210 cls$="        "+chr$(157)+chr$(157)+chr$(157)+chr$(157)
1211 cls$=cls$+chr$(157)+chr$(157)+chr$(157)+chr$(157)
1215 s$=chr$(19)+chr$(17)+chr$(17)+chr$(17)+chr$(17)+chr$(17)+chr$(17)


1220 for i=1 to t : rem primo ciclo di tutti i tentativi
1225 rem ?chr$(145);"                 ":rem cancellaconferma
1227 poke 646, 14
1228 ?chr$(19);"numero:";nc
1230 ?chr$(19):?:?:?" tentativo n.";i
1235 ?:?" inserisci il tuo"
1240 ?" codice:";cls$;
1245 for j=1 to l
1250 get pin(j) : if pin(j)=0 then 1250
1255 ?pin(j);chr$(157);
1260 next j
1270 in=((pin(1)*10+pin(2))*10+pin(3))*10+pin(4)
1280 ?:?" confermi? (s/n):"
1290 get w$ : if (w$<>"s" and w$<>"n") then 1290
1300 ?chr$(145);"                 ":rem cancellaconferma
1310 if w$="n" then 1225

1320 ?s$;tab(21);i;chr$(155);"B";chr$(154);in;chr$(155);"B ";
1330 s$=s$+chr$(17)+chr$(17)

1400 rem ***** controlli *****
1410 rem ***** controllo neri *****
1415 pneri=0 : pbia=0
1420 for j=1 to l : boolneri(j)=0 : next j : rem iniz
1430 for j=1 to l 
1440 if c(j) = pin(j) then 1460
1450 goto 1480
1460 boolneri(j)=1
1470 ?chr$(144)"Q";
1475 pneri=pneri+1
1480 next j

1500 rem ***** controllo bianchi *****
1510 dvu = 1 : rem dimensione vettore univoci
1520 for j=1 to l
1530 for k=1 to l
1540 if ((pin(j)=c(k)) and (boolneri(j)=0) and (boolneri(k)=0)) then 1560
1550 goto 1650
1560 flag=0
1570 for x=1 to dvu-1
1580 if vcu(x)=pin(j) then 1585
1582 goto 1590
1585 flag=flag+1
1590 next x
1600 if flag=0 then 1620
1610 goto 1650 
1620 vcu(dvu)=pin(j) 
1630 dvu=dvu+1
1650 next k  
1660 next j

1665 if dvu=1 then goto 1700
1670 for j=1 to dvu-1
1680 pbia = pbia + 1
1690 next j

1695 vcu(1)=0

1700 if pneri=0 then 1715
1710 goto 1800
1715 if dvu=1 then 1800
1720 for j=1 to dvu-1
1725 cv1 = 0
1735 for k=1 to l
1740 if vcu(j)=c(k) then cv1 = cv1 + 1
1745 next k
1750 cv2 = 0
1755 for k=1 to l
1760 if vcu(j)=pin(k) then cv2 = cv2 + 1
1765 next k
1770 if (cv1=2 and cv2 = 2) then 1780
1775 goto 1790
1780 vcu(dvu) = vcu(j) : dvu = dvu+1 : pbia = pbia + 1
1790 next j

1800 if pbia=0 then goto 1840
1810 for j=1 to pbia
1820 ?chr$(5)"Q";
1830 next j
1840 rem

1900 if pneri=4 then i=10

2000 next i

2050 for i=1 to 30 : ? : next i
2055 ?chr$(147)
2060 if pneri=4 then 2100 :rem hai vinto
2070 goto 2200 :rem hai perso

2100 ?"hai vinto!!!!!!"

2200 ?"hai perso :("